{
  workspaceRoot,
  inputs,
  self,
  ...
}:
{
  perSystem =
    {
      pkgs,
      config,
      lib,
      ...
    }:
    let
      # The dumped Metal toolchain
      apple-metal-toolchain = pkgs.callPackage ../../packages/apple-metal-toolchain.nix { };

      # The native Nixpkgs SDK
      apple-sdk = pkgs.apple-sdk_15;

      # Combine them into a single directory that looks like /Applications/Xcode.app/Contents/Developer
      xcode-combined = pkgs.symlinkJoin {
        name = "xcode-combined";
        paths = [
          apple-metal-toolchain
          apple-sdk
        ];
      };
      commonProjectEnv = {
        PYO3_PYTHON = "${pkgs.python3}/bin/python";
        PROTOC = "${pkgs.protobuf}/bin/protoc";
        SSL_CERT_FILE = "${pkgs.cacert}/etc/ssl/certs/ca-bundle.crt";
        NIX_SSL_CERT_FILE = "${pkgs.cacert}/etc/ssl/certs/ca-bundle.crt";
      };
      cargoLockText = builtins.readFile (workspaceRoot + "/Cargo.lock");
      cargoLockLines = lib.splitString "\n" cargoLockText;
      cargoLockGitRev =
        repoUrl:
        let
          prefix = "source = \"git+${repoUrl}?rev=";
          matches = lib.filter (line: lib.hasPrefix prefix line) cargoLockLines;
        in
        if matches == [ ] then
          throw "failed to resolve git rev for ${repoUrl} from Cargo.lock"
        else
          builtins.elemAt (lib.splitString "#" (lib.removePrefix prefix (builtins.head matches))) 0;
      lanceRev = cargoLockGitRev "https://github.com/lancedb/lance.git";
      lanceSrc = pkgs.fetchzip {
        url = "https://github.com/lancedb/lance/archive/${lanceRev}.tar.gz";
        hash = "sha256-Cp93QTsTrTkXizWYoZtFz88R3lX7+MmYN4E9JYBsyps=";
      };
      lanceVendorFixup = import ../../lib/lance-vendor-fixup.nix { inherit lanceSrc; };
      restoreWorkspaceCargoLock = ''
        echo "restoring workspace Cargo.lock for vendored git source replacement"
        cp -f ${workspaceRoot}/Cargo.lock Cargo.lock
      '';
      commonProjectDrvConfig = {
        deps.fetchurl = import ../../lib/crates-static-fetchurl.nix pkgs.fetchurl;
        mkDerivation = {
          nativeBuildInputs = [
            pkgs.pkg-config
            pkgs.protobuf
          ];
          buildInputs = [
            pkgs.openssl
            pkgs.cacert
          ];
          postConfigure = restoreWorkspaceCargoLock;
          preBuild = restoreWorkspaceCargoLock;
        };
        env = commonProjectEnv;
      };
      commonProjectDepsDrvConfig = lib.recursiveUpdate commonProjectDrvConfig {
        mkDerivation =
          let
            runCargoDepsFixup = ''
              ${restoreWorkspaceCargoLock}
              ${lanceVendorFixup}
              echo "patching Lance cargo vendor manifests"
              fix_lance_vendor_dir "''${cargoVendorDir:-$TMPDIR/nix-vendor}"
            '';
          in
          {
            postConfigure = runCargoDepsFixup;
            preBuild = runCargoDepsFixup;
          };
      };
    in
    {
      _module.args.apple-metal-toolchain = apple-metal-toolchain;

      nci.projects."cyber-xiuxian-workshop" = {
        path = workspaceRoot;
        export = true;
        drvConfig = commonProjectDrvConfig;
        depsDrvConfig = commonProjectDepsDrvConfig;
      };
      # configure crates
      nci.crates = {
        # "xiuxian-llm" = {
        #   depsDrvConfig = {
        #     mkDerivation.nativeBuildInputs = lib.optionals pkgs.stdenv.hostPlatform.isDarwin [
        #       apple-metal-toolchain
        #       pkgs.xcbuild
        #     ];
        #     mkDerivation.buildInputs = lib.optionals pkgs.stdenv.hostPlatform.isDarwin [
        #       apple-sdk
        #     ];
        #     env = lib.optionalAttrs pkgs.stdenv.hostPlatform.isDarwin {
        #       MISTRALRS_METAL_PRECOMPILE = "1";
        #       # Point DEVELOPER_DIR to the combined symlink forest
        #       DEVELOPER_DIR = "${xcode-combined}";
        #       # Point SDKROOT to the macOS SDK within that forest
        #       SDKROOT = "${xcode-combined}/Platforms/MacOSX.platform/Developer/SDKs/MacOSX.sdk";
        #     };
        #   };
        # };
        "xiuxian-wendao" = {
          drvConfig.mkDerivation.nativeBuildInputs = [ pkgs.protobuf ];
          profiles.release.runTests = false;
        };
        "xiuxian-zhenfa" = {
          profiles.release.runTests = false;
          drvConfig.mkDerivation = {
            nativeBuildInputs = [ pkgs.pkg-config ];
            buildInputs = [
              pkgs.libxml2
              pkgs.cacert
            ];
          };
          drvConfig.env = {
            SSL_CERT_FILE = "${pkgs.cacert}/etc/ssl/certs/ca-bundle.crt";
            NIX_SSL_CERT_FILE = "${pkgs.cacert}/etc/ssl/certs/ca-bundle.crt";
          };
          depsDrvConfig.mkDerivation = {
            buildInputs = [
              pkgs.libxml2
              pkgs.cacert
            ];
          };
          depsDrvConfig.env = {
            SSL_CERT_FILE = "${pkgs.cacert}/etc/ssl/certs/ca-bundle.crt";
            NIX_SSL_CERT_FILE = "${pkgs.cacert}/etc/ssl/certs/ca-bundle.crt";
          };
        };
        "xiuxian-qianji" = {
          drvConfig = {
            mkDerivation = {
              buildInputs = [
                pkgs.protobuf
                pkgs.libxml2
                pkgs.valkey
              ];
            };
            env.VALKEY_SERVER_BIN = "${pkgs.valkey}/bin/valkey-server";
          };
          depsDrvConfig = {
            mkDerivation = {
              buildInputs = [
                pkgs.protobuf
                pkgs.libxml2
                pkgs.valkey
              ];
            };
            env.VALKEY_SERVER_BIN = "${pkgs.valkey}/bin/valkey-server";
          };
        };
        "xiuxian-lance" = {
          drvConfig.mkDerivation.nativeBuildInputs = [ pkgs.protobuf ];
          drvConfig.env.PROTOC = "${pkgs.protobuf}/bin/protoc";
          depsDrvConfig = lib.recursiveUpdate commonProjectDepsDrvConfig {
            env.PROTOC = "${pkgs.protobuf}/bin/protoc";
          };
        };
        "xiuxian-memory-engine" = {
          profiles.release.runTests = false;
        };
        "xiuxian-vector" = {
          drvConfig.mkDerivation.nativeBuildInputs = [ pkgs.protobuf ];
          drvConfig.env.PROTOC = "${pkgs.protobuf}/bin/protoc";
          depsDrvConfig = lib.recursiveUpdate commonProjectDepsDrvConfig {
            env.PROTOC = "${pkgs.protobuf}/bin/protoc";
          };
        };
      };

      packages.xiuxian-core-rs-python-bindings = pkgs.callPackage ../../packages/xiuxian-core-rs.nix {
        inherit workspaceRoot;
        cargoDeps =
          config.nci.outputs."xiuxian-core-rs".packages.release.config.rust-cargo-vendor.vendoredSources;
        version = config.nci.outputs."xiuxian-core-rs".packages.release.config.version;
      };
      packages.xiuxian-wendao-client = config.nci.outputs."xiuxian-wendao-client".packages.release;
    };
}
